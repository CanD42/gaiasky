apply plugin: "java"
apply plugin: "jetty"

gwt {
    gwtVersion='2.7.0' // Should match the gwt version used for building the gwt backend
    maxHeapSize="2G" // Default 256m is not enough for gwt compiler. GWT is HUNGRY
    minHeapSize="1G"

    src = files(file("src/")) // Needs to be in front of "modules" below.
    modules 'gaia.cu9.ari.gaiaorbit.GdxDefinition'
    devModules 'gaia.cu9.ari.gaiaorbit.GdxDefinitionSuperdev'
    project.webAppDirName = 'webapp'

    compiler {
        strict = true;
        enableClosureCompiler = true;
        disableCastChecking = true;
    }
}

compileGwt {
    minHeapSize = "1G";
    maxHeapSize = "2G";
}

task draftRun(type: JettyRunWar) {
    dependsOn draftWar
    dependsOn.remove('war')
    webApp=draftWar.archivePath
    daemon=true
}

task superDev(type: de.richsource.gradle.plugins.gwt.GwtSuperDev) {
    dependsOn draftRun
    doFirst {
    	gwt.modules = gwt.devModules
    }
}

task dist(dependsOn: [clean, compileGwt]) {
    doLast {
        file("build/dist").mkdirs()
        copy {
            from "build/gwt/out"
            into "build/dist"
        }
        copy {
            from "webapp"
            into "build/dist"
            }
        copy {
            from "war"
            into "build/dist"
        }
    }
}

task afterGwtCompileFull << {
    // update logo
    delete 'war/html/logo.png'
    copy {
        from '../android/assets/img/gaiasandboxlogo.png'
        into 'war/html/'
    }
    file('war/html/gaiasandboxlogo.png').renameTo(file('war/html/logo.png'))
    
    // update index.html
    delete 'war/index.html'
    copy {
        from 'webapp/index.html'
        into 'war/'
    }
    
    // delete unused files
    delete 'war/assets/data/data-lite.json',
            'war/assets/data/extra-lite.json',
            'war/assets/data/planets-lite.json',
            'war/assets/data/satellites-lite.json'
    
}

task afterGwtCompileLite << {
    // update logo
    delete 'war/html/logo.png'
    copy {
        from '../android/assets/img/gaiasandboxlogo.png'
        into 'war/html/'
    }
    file('war/html/gaiasandboxlogo.png').renameTo(file('war/html/logo.png'))
    
    // update index.html
    delete 'war/index.html'
    copy {
        from 'webapp/index-lite.html'
        into 'war/'
    }
    file('war/index-lite.html').renameTo(file('war/index.html'))
    
    // delete unused files
    delete 'war/assets/conf', 
            'war/assets/data/boundaries.csv',
            'war/assets/data/constel.csv',
            'war/assets/data/data.json',
            'war/assets/data/locations_earth.json',
            'war/assets/data/locations_moon.json',
            'war/assets/data/locations.json',
            'war/assets/data/moons.json',
            'war/assets/data/orbits_asteroid.json',
            'war/assets/data/orbits_moon.json',
            'war/assets/data/planets.json',
            'war/assets/data/satellites.json',
            'war/assets/data/models',
            'war/assets/data/tex/callisto.jpg',
            'war/assets/data/tex/ceres.jpg',
            'war/assets/data/tex/deimos.jpg',
            'war/assets/data/tex/earth-night.jpg',
            'war/assets/data/tex/earth-specular.jpg',
            'war/assets/data/tex/earth.jpg',
            'war/assets/data/tex/europa.jpg',
            'war/assets/data/tex/ganymede.jpg',
            'war/assets/data/tex/io.jpg',
            'war/assets/data/tex/jupiter.jpg',
            'war/assets/data/tex/mars.jpg',
            'war/assets/data/tex/mercury.jpg',
            'war/assets/data/tex/milky_way_small.png',
            'war/assets/data/tex/moon.jpg',
            'war/assets/data/tex/mw_small.jpg',
            'war/assets/data/tex/mw.jpg',
            'war/assets/data/tex/mw.png',
            'war/assets/data/tex/neptune.jpg',
            'war/assets/data/tex/pallas.jpg',
            'war/assets/data/tex/phobos.jpg',
            'war/assets/data/tex/saturn-ring.jpg',
            'war/assets/data/tex/saturn.jpg',
            'war/assets/data/tex/uranus.jpg',
            'war/assets/data/tex/venus.jpg',
            'war/assets/data/tex/vesta.jpg',
            'war/assets/scripts'
            
}

draftWar {
   from "war"
}

task addSource << {
	sourceSets.main.compileClasspath += files(project(':core').sourceSets.main.allJava.srcDirs)
}

tasks.compileGwt.dependsOn(addSource)
tasks.draftCompileGwt.dependsOn(addSource)

sourceCompatibility = 1.7
sourceSets.main.java.srcDirs = [ "src/" ]


eclipse.project {
    name = appName + "-html"
}
